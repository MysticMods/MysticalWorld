buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            url = "https://repo.spongepowered.org/repository/maven-public/"
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.0.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}
plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'maven-publish'
apply plugin: 'eclipse'
apply plugin: 'org.spongepowered.mixin'

version = "${minecraft_version}-${mod_version}"
if (System.getenv('BUILD_NUMBER') != null) {
    version = "${minecraft_version}-${mod_version}-" + System.getenv('BUILD_NUMBER')
}
group = 'mysticmods.mysticalworld' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'mysticalworld'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.


minecraft {
    mappings channel: 'snapshot', version: "${mcp_mappings}"

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            //property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
            arg "-mixin.config=mysticalworld.mixins.json"
            property 'mixin.env.disableRefMap', 'true'

            mods {
                mysticalworld {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            //property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
            arg "-mixin.config=mysticalworld.mixins.json"
            property 'mixin.env.disableRefMap', 'true'

            mods {
                mysticalworld {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            environment 'target', 'fmluserdevdata'

            // Recommended logging data for a userdev environment
            //property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
            arg "-mixin.config=mysticalworld.mixins.json"
            property 'mixin.env.disableRefMap', 'true'

            args '--mod', 'mysticalworld', '--all', '--output', file('src/generated/resources/'), '--existing', sourceSets.main.resources.srcDirs[0]

            mods {
                mysticalworld {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    maven {
        url 'https://maven.mcmoddev.com'
    }
    maven {
        name = "Jared"
        url = "https://maven.blamejared.com"
    }
    maven {
        url "http://maven.tterrag.com"
    }
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        name = "JER"
        url = "https://dl.bintray.com/way2muchnoise/dev"
    }
    maven { url = 'https://repo.spongepowered.org/maven/' }

    maven { url = 'https://dist.creeper.host/Sponge/maven' }
    jcenter()
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
}

configurations {
    shade
}
dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    compile fg.deobf("vazkii.patchouli:Patchouli:${patchouli_version}")
    compile fg.deobf("mezz.jei:jei-${jei_version}")

    def registrate = "com.tterrag.registrate:Registrate:${registrate_version}"
    implementation fg.deobf(registrate)
    shade registrate

    def noobutil = "noobanidus.libs:noobutil:${minecraft_version}-${noobutil_version}"
    implementation fg.deobf(noobutil)
    shade noobutil

    compile 'org.spongepowered:mixin:0.8'
    annotationProcessor 'org.spongepowered:mixin:0.8:processor'

/*    runtimeOnly fg.deobf("tg:blame:1.7.5")*/
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "mysticalworld",
                "Specification-Vendor"    : "mysticmodding",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "mysticmodding",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs"            : "mysticalworld.mixins.json"
        ])
    }
}

shadowJar {
    configurations = [project.configurations.shade]
    relocate 'com.tterrag.registrate', 'mysticmods.mysticalworld.repack.registrate'
    relocate 'noobanidus.libs.noobutil', 'noobanidus.libs.repack_mysticalworld.noobutil'
    classifier ''
}

jar.classifier = 'nodep'

reobf {
    shadowJar {}
}

build.dependsOn reobfShadowJar

task apiJar(type: Jar) {
    from(sourceSets.main.output)
    include 'mysticmods/mysticalworld/api/**'

    classifier = 'api'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

build.dependsOn apiJar
build.dependsOn deobfJar

mixin {
    add sourceSets.main, "mysticalworld.refmap.json"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java

            artifact apiJar {
            }

            artifact shadowJar {
            }

            pom.withXml {
                asNode().dependencies.dependency.each {
                    dep -> dep.parent().remove(dep)
                }
            }
        }
    }
    repositories {
        maven {
            url "file:///" + System.getenv("local_maven")
        }
    }
}
